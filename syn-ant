#!/usr/bin/env perl
use strict;
use warnings;

use WordNet::QueryData;

my $wn = WordNet::QueryData->new(
    noload => 1,
    dir    => '/usr/local/Cellar/wordnet/3.1/dict/',
);

my $word = shift || 'small';
my $pos  = shift || 'a';

for my $sense (1 .. 100) {
    my $w = "$word#$pos#$sense";

    my %query = (
        antonyms   => [ $wn->queryWord($w, 'ants') ],
        cause      => [ $wn->querySense($w, 'caus') ],
        entailment => [ $wn->querySense($w, 'enta') ],
        holonyms   => [ $wn->querySense($w, 'holo') ],
        hypernyms  => [ $wn->querySense($w, 'hypes') ],
        hyponyms   => [ $wn->querySense($w, 'hypos') ],
        meronyms   => [ $wn->querySense($w, 'mero') ],
        similar    => [ $wn->querySense($w, 'sim') ],
        synsets    => [ $wn->querySense($w, 'syns') ],
    );

    my $text = '';

    for my $key (sort keys %query) {
        $text .= ucfirst $key . ":\n\t" . join(', ', $query{$key}->@*) . "\n"
            if $query{$key}->@*;
    }

    print "* Word:\n\t$w\n$text\n" if $text;
}
