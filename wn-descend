#!/usr/bin/env perl
use strict;
use warnings;

use WordNet::QueryData;

my $term  = shift || 'man';
my $pos   = shift || 'n';
my $sense = shift || '1';
my $depth = shift || 0;

$term .= '#' . $pos . '#' . $sense;

my $wn = WordNet::QueryData->new(
    dir    => '/usr/local/opt/wordnet/dict',
    noload => 1
#    verbose => 1,
);

descend($term);

sub descend {
    my ($term, $i) = @_;

    $i //= 0;

    return if $depth && $i > $depth;

    my @hypos = $wn->querySense($term, 'hypo');

    my @syns = $wn->querySense($term, 'syns');
    my @synonyms;
    for my $syn (@syns) {
        push @synonyms, $syn
            unless $syn eq $term;
    }
    my $syns = @synonyms ? " (@synonyms)" : '';

    print "\t" x $i, "$term$syns", @hypos ? ':' : '', "\n";

    for my $sense (@hypos) {
        descend($sense, $i + 1);
    }
}
