#!/usr/bin/env perl
use strict;
use warnings;

use WordNet::QueryData;

my $term  = shift || 'man';
my $pos   = shift || 'n'; # n: noun, v: verb, a: adjective, r: adverb
my $sense = shift || '1';
my $depth = shift || 0;

$term .= '#' . $pos . '#' . $sense;

my $wn = WordNet::QueryData->new(
    dir    => '/usr/local/opt/wordnet/dict',
    noload => 1
#    verbose => 1,
);

descend($term);

sub descend {
    my ($word, $i) = @_;

    $i //= 0;

    return if $depth && $i > $depth;

    my @hypos = $wn->querySense($word, 'hypo');

    my @syns = $wn->querySense($word, 'syns');
    my @synonyms;
    for my $syn (@syns) {
        push @synonyms, $syn
            unless $syn eq $word;
    }
    my $syns = @synonyms ? " (@synonyms)" : '';

    print "\t" x $i, "$word$syns", @hypos ? ':' : '', "\n";

    descend($_, $i + 1)
        for @hypos;
}
